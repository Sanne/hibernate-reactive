description = 'Hibernate Reactive Verticle Integration Tests'

ext {
	jacksonDatabindVersion = '2.15.2'
	jbossLoggingVersion = '3.5.0.Final'
	assertjVersion = '3.24.2'
	vertxWebVersion = '4.4.4'
}

dependencies {
	implementation project( ':hibernate-reactive-core' )
	implementation "io.vertx:vertx-web:${vertxWebVersion}"
	implementation "io.vertx:vertx-web-client:${vertxWebVersion}"

	runtimeOnly "io.vertx:vertx-pg-client:${vertxVersion}"
	// The Pg client requires this dependency
	runtimeOnly "com.ongres.scram:client:2.1"
	runtimeOnly "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"

	// logging
	implementation "org.jboss.logging:jboss-logging:${jbossLoggingVersion}"

	// Testcontainers
	implementation "org.testcontainers:postgresql:${testcontainersVersion}"

	// Testing
//	testImplementation "org.assertj:assertj-core:${assertjVersion}"
//	testImplementation "io.vertx:vertx-unit:${vertxVersion}"
}

buildscript {
	repositories {
		// Example: ./gradlew build -PenableMavenLocalRepo
		if ( project.hasProperty( 'enableMavenLocalRepo' ) ) {
			mavenLocal()
		}

		mavenCentral()
	}
}

// Configuration for the tests
// This is the same as the one in hibernate-reactive-core
tasks.withType( Test ).configureEach {
	defaultCharacterEncoding = "UTF-8"
	testLogging {
		showStandardStreams = project.hasProperty( 'showStandardOutput' )
		showStackTraces = true
		exceptionFormat = 'full'
		displayGranularity = 1
		events = ['PASSED', 'FAILED', 'SKIPPED']
	}
	systemProperty 'docker', project.hasProperty( 'docker' ) ? 'true' : 'false'
	systemProperty 'org.hibernate.reactive.common.InternalStateAssertions.ENFORCE', 'true'
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	manifest {
		attributes 'Main-Class': 'org.hibernate.reactive.it.verticle.StartVerticle'
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

test {
	// We only want to test this on Postgres
	afterSuite { desc, result ->
		loggingSummary( 'PostgreSQL', result, desc )
	}
	doFirst {
		systemProperty 'db', 'PostgreSQL'
	}
}

// Print a summary of the results of the tests (number of failures, successes and skipped)
// This is the same as the one in hibernate-reactive-core
def loggingSummary(db, result, desc) {
	if ( !desc.parent ) { // will match the outermost suite
		def output = "${db} results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
		def repeatLength = output.length() + 1
		logger.lifecycle '\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength)
	}
}

tasks.register( "startVertx", JavaExec ) {
	group = "Execution"
	description = "Starts the Vert.x app"
	classpath = sourceSets.main.runtimeClasspath
	mainClass = "org.hibernate.reactive.it.verticle.StartVerticle"
}
